---
title: 'AWS IAM rol con Terraform'
author: Victor Silva
date: 2022-08-27T01:03:56+00:00
layout: post
permalink: /IAM-rol-AWS-terraform/
excerpt: 'En esta entrada, vamos a ver tres formas de crear una pol√≠tica y un rol de IAM en la nube de AWS utilizando Terraform. Tambi√©n proporciona una forma sencilla de conectar la pol√≠tica seleccionada a un rol de IAM. Supongo que ya conoces un poco de terraform y la nube de AWS, por lo que no explicar√© los conceptos b√°sicos.'
categories:
  - AWS
  - IAM
  - Terraform
tags:
  - AWS
  - IAM
  - Terraform
---

En esta entrada, vamos a ver tres formas de crear una pol√≠tica y un rol de IAM en AWS utilizando Terraform. Tambi√©n veremos una forma sencilla de conectar la pol√≠tica seleccionada a un rol de IAM en particular. Supongo que ya conoces un poco de Terraform y la nube de AWS, por lo que no explicar√© los conceptos b√°sicos.

## M√©todo 1: `aws_iam_policy_document`

La primera forma es crear un elemento **data** en Terraform `aws_iam_policy_document`. Esta forma de implementaci√≥n es probablemente la m√°s com√∫n en versiones anteriores de TF. A continuaci√≥n, describir√© en qu√© consiste el archivo completo, que le permite crear una pol√≠tica, un rol y combinar estos dos recursos. A menudo agrupo diferentes tipos de recursos en archivos para que me sea m√°s f√°cil encontrarlos en el proyecto. En este ejemplo, presento el contenido del archivo **iam.tf**.

### data `aws_iam_policy_document`

De esta forma, creo un elemento de datos y en √©l especifico c√≥mo aws_iam_policy_documentdebe ser la pol√≠tica de IAM . Este es un elemento clave y vale la pena dedicarle m√°s tiempo. Recomiendo otorgar siempre solo los permisos m√≠nimos necesarios. Desafortunadamente, esto hace que el rol sea menos universal , pero es un precio que, en mi opini√≥n, vale la pena pagar, aumentando as√≠ significativamente la seguridad .

Ni siquiera estoy hablando de una acci√≥n deliberada aqu√≠, pero si, por ejemplo, damos acceso completo a todos los S3 en lugar de uno y nuestra funci√≥n no funciona como se esperaba, puede tener consecuencias muy graves.

Buen consejo: no haga que todos los usuarios sean administradoresüòâ

En tal documento, por supuesto, puedo definir varias expresiones . En el siguiente ejemplo, permito:

    GetObject y DeleteObject en la carpeta de origen de S3 Bucket especificada,
    Fuente S3 ListBucket y el contenido de su carpeta √∫nica,
    PutObjects en una sola carpeta de destino de S3

### resource `aws_iam_policy`

Luego creo una pol√≠tica aws_iam_policyy agrego la creada previamente aws_iam_policy_document.
recursoaws_iam_role

El siguiente paso es crear un rol de IAM aws_iam_role. Posteriormente, el rol se puede asignar a la m√°quina EC2, la funci√≥n Lambda, etc.

### resource `aws_iam_role_policy_attachment`

El √∫ltimo paso es agregar la pol√≠tica al rol de IAM `aws_iam_role_policy_attachment`. Si quisiera agregar m√°s pol√≠ticas de IAM a un rol, crear√≠a m√°s recursos aws_iam_role_policy_attachment. En ellos doy el mismo nombre de rol y el siguiente nombre de la pol√≠tica que quiero agregar.

{% highlight terraform%}
data "aws_iam_policy_document" "S3_automation_move_objects" {
  statement {
    sid = "allowS3"
    actions = [
      "s3:GetObject",
      "s3:DeleteObject",
    ]
    resources = [
      "arn:aws:s3:::test-ndgegy4364gdu-source-bucket/images/*"
    ]
  }

  statement {
    sid = "allowListBucket"
    actions = [
      "s3:ListBucket",
    ]
    resources = [
      "arn:aws:s3:::test-ndgegy4364gdu-source-bucket",
      "arn:aws:s3:::test-ndgegy4364gdu-source-bucket/images/*"
    ]
  }

  statement {
    sid = "putObject"
    actions = [
      "s3:PutObject",
    ]
    resources = [
      "arn:aws:s3:::test-ndgegy4364gdu-destination-bucket/images/*"
    ]
  }

}

resource "aws_iam_policy" "S3_automation_move_objects" {
  name        = "S3_automation_move_objects"
  description = "test - access to source and destination S3 bucket"
  path        = "/"

  policy = data.aws_iam_policy_document.S3_automation_move_objects.json
}

resource "aws_iam_role" "S3_automation_move_objects" {
  name        = "S3_automation_move_objects"
  description = "test - access to source and destination S3 bucket"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          Service = "ec2.amazonaws.com"
        }
      },
    ]
  })
}

resource "aws_iam_role_policy_attachment" "S3_automation_move_objects" {
  role       = aws_iam_role.S3_automation_move_objects.name
  policy_arn = aws_iam_policy.S3_automation_move_objects.arn
}
{% endhighlight %}

# M√©todo 2: funci√≥n jsonencode

Esta vez, para crear una pol√≠tica de IAM en Terraform, no utilizo elementos adicionales. Los permisos se definen directamente en aws_iam_policy. Estoy usando una funci√≥n jsonencode especial aqu√≠ . Esta funci√≥n asigna valores de Terraform a valores JSON . En este caso tampoco tom√© el camino f√°cil y defin√≠ algunas expresiones. Esto lo hace m√°s como una soluci√≥n real.

La creaci√≥n aws_iam_roley adici√≥n de una pol√≠tica de IAM a un rol de IAM se realiza de la misma manera que en el primer m√©todo, es decir, utilizando aws_iam_role_policy_attachment.

{% highlight terraform%}
resource "aws_iam_policy" "S3_automation_move_objects" {
  name        = "S3_automation_move_objects"
  description = "test - access to source and destination S3 bucket"
  path        = "/"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = [
			"s3:GetObject",
			"s3:DeleteObject",
        ]
        Effect   = "Allow"
        Resource = "arn:aws:s3:::test-ndgegy4364gdu-source-bucket1/images/*"
      },
      {
        Action = [
			"s3:ListBucket",
        ]
        Effect   = "Allow"
        Resource = [
			"arn:aws:s3:::test-ndgegy4364gdu-source-bucket1",
			"arn:aws:s3:::test-ndgegy4364gdu-source-bucket1/images/*"
		]
      },
      {
        Action = [
			"s3:putObject",
        ]
        Effect   = "Allow"
        Resource = "arn:aws:s3:::test-ndgegy4364gdu-destination-bucket1/images/*"
      },
    ]
  })
}

resource "aws_iam_role" "S3_automation_move_objects" {
  name        = "S3_automation_move_objects"
  description = "test - access to source and destination S3 bucket"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          Service = "ec2.amazonaws.com"
        }
      },
    ]
  })
}

resource "aws_iam_role_policy_attachment" "S3_automation_move_objects" {
  role       = aws_iam_role.S3_automation_move_objects.name
  policy_arn = aws_iam_policy.S3_automation_move_objects.arn
}
{% endhighlight %}

# M√©todo 3 - EOF

Tambi√©n puedes usar el tercer m√©todo, que es muy similar al del punto 2. De nuevo, no define elementos adicionales. Sin embargo, en lugar de usar la funci√≥n, pegamos el contenido del archivo JSON .

Si tiene pol√≠ticas de IAM definidas en formato JSON , puede agregar el contenido de dicha pol√≠tica al recurso sin ning√∫n cambio aws_iam_policy.

Crear un rol de IAM en Terraform es igual que en las dos formas anteriores. Esta vez tambi√©n combino la pol√≠tica con el rol usando aws_iam_role_policy_attachment.

{% highlight terraform%}
resource "aws_iam_policy" "S3_automation_move_objects" {
  name        = "S3_automation_move_objects"
  description = "test - access to source and destination S3 bucket"
  path        = "/"

  policy = <<EOF
{
	"Version": "2012-10-17",
    "Statement": [
        {
            "Action": [
                "s3:GetObject",
                "s3:DeleteObject"
            ],
            "Effect": "Allow",
            "Resource": "arn:aws:s3:::test-ndgegy4364gdu-source-bucket1/images/*"
        },
        {
            "Action": [
                "s3:ListBucket"
            ],
            "Effect": "Allow",
            "Resource": [
                "arn:aws:s3:::test-ndgegy4364gdu-source-bucket1",
                "arn:aws:s3:::test-ndgegy4364gdu-source-bucket1/images/*"
            ]
        },
        {
            "Action": [
                "s3:putObject"
            ],
            "Effect": "Allow",
            "Resource": "arn:aws:s3:::test-ndgegy4364gdu-destination-bucket1/images/*"
        }
    ],
    "Version": "2012-10-17"
}
EOF
}

resource "aws_iam_role" "S3_automation_move_objects" {
  name        = "S3_automation_move_objects"
  description = "test - access to source and destination S3 bucket"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Sid    = ""
        Principal = {
          Service = "ec2.amazonaws.com"
        }
      },
    ]
  })
}

resource "aws_iam_role_policy_attachment" "S3_automation_move_objects" {
  role       = aws_iam_role.S3_automation_move_objects.name
  policy_arn = aws_iam_policy.S3_automation_move_objects.arn
}
{% endhighlight %}

# PRUEBA

Al principio, es bueno aprender Terraform en un c√≥digo que funciona, mejorarlo y adaptarlo a sus necesidades.

Para verificar si los m√©todos anteriores funcionan correctamente, simplemente reemplace los nombres de los dep√≥sitos S3 con los que ha creado usted mismo:

* Test-ndgegy4364gdu-source-bucket: mi dep√≥sito de origen desde el que descargo y elimino datos
* Test-ndgegy4364gdu-destination-bucket: mi dep√≥sito de destino al que estoy transfiriendo datos

<img>

El c√≥digo anterior crear√°:

* Pol√≠tica de IAM denominada 'S3_automation_move_objects',
* Rol de IAM denominado 'S3_automation_move_objects',
* Combinar√° la pol√≠tica de IAM con el rol de IAM

# Resumen

Se han probado y verificado las tres formas de crear una pol√≠tica de IAM en Terraform. En tu trabajo del d√≠a a d√≠a, puedes elegir el que m√°s te convenga o uno que alguien m√°s haya usado antes por coherencia.

Si conoce alguna otra forma de crear una pol√≠tica de IAM en Terraform, puede compartirla en el comentario.

Happy scripting!